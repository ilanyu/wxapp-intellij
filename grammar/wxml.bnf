{
  parserClass="com.github.zxj5470.weapp.psi.wxml.WxmlParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Wxml"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.zxj5470.weapp.psi"
  psiImplPackage="com.github.zxj5470.weapp.psi.impl.wxml"

  tokenTypeClass="com.github.zxj5470.weapp.psi.wxml.WxmlTokenType"
  elementTypeHolderClass="com.github.zxj5470.weapp.psi.wxml.WxmlTypes"
  elementTypeClass="com.github.zxj5470.weapp.psi.wxml.WxmlElementType"
}

wxmlFile ::= ws (tags)*
ws ::= endOfLine*
private tags ::=
	(tagOpen ws content? tagClose) ws
content::= tags ws

endOfLine::= WHITE_SPACE | EOL | comment

tagOpen ::= LT tagName ws attrList? GT
tagClose ::= LT_BEGIN tagName GT
comment ::= COMMENT_BEGIN COMMENT_END
attrList ::= attr*
attr ::= attrSingleQuoted | attrDoubleQuoted | justName

attrSingleQuoted ::= attrs assign S_QUOTE attrSingleQuotedValue S_QUOTE
attrDoubleQuoted ::= attrs assign D_QUOTE attrDoubleQuotedValue D_QUOTE

justName ::= chars
tagName ::= chars

attrs::= wxAttr | simpleAttr
wxAttr ::= wx (COLON wxKeyword)*
simpleAttr::= chars (MINUS chars)*

wx::='wx'
wxKeyword::=chars

attrSingleQuotedValue ::= (chars|digits)+
attrDoubleQuotedValue ::= (chars|digits)+